pluginManagement {
  repositories {
    mavenCentral() {
      content {
        excludeGroup 'com.axelor'
      }
    }
    maven {
      url 'https://repository.axelor.com/nexus/repository/maven-public/'
    }
  }
  plugins {
    id 'com.axelor.app' version '7.3.3'
  }
}

plugins {
  id 'com.github.node-gradle.node' version '3.5.1' apply false
}


dependencyResolutionManagement {
  repositories {
    mavenCentral() {
      content {
        excludeGroup 'com.axelor'
      }
    }
    maven {
      url 'https://repository.axelor.com/nexus/repository/maven-public/'
    }
    // Declare the Node.js download repository
    ivy {
      name = "Node.js"
      setUrl("https://nodejs.org/dist/")
      patternLayout {
        artifact("v[revision]/[artifact](-v[revision]-[classifier]).[ext]")
      }
      metadataSources {
        artifact()
      }
      content {
        includeModule("org.nodejs", "node")
      }
    }
  }
}

//Include all modules
rootProject.name = "open-platform-demo"

include 'modules:axelor-account'
include 'modules:axelor-base'
include 'modules:axelor-crm'
include 'modules:axelor-sale'
include 'modules:axelor-stock'
include 'modules:axelor-supplychain'
